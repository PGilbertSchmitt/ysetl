program_input = _{ SOI ~ (program | program_missing_expr) ~ EOI }
repl_input = _{ SOI ~ expr ~ EOI }

program = { "program" ~ atom ~ semicolon ~ expr_block ~ semicolon?}
program_missing_expr = { "program" ~ atom ~ semicolon? } // Example of parse-error-catcher

WHITESPACE = _{ " " | "\t" | NEWLINE }
__ = { WHITESPACE }
COMMENT = _{ dbl_slash ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }

// Keywords

null = { "null" }
true_ = { "true" }
false_ = { "false" }
newat = { "newat" }
func = _{ "func" }
if_ = @{ "if" ~ __ }
case_ = @{ "case" ~ __ }

mod_ = @{ "mod" ~ __ }
div = @{ "div" ~ __ }
inter = @{ "inter" ~ __ }
with = @{ "with" ~ __ }
less = @{ "less" ~ __ }
union_ = @{ "union" ~ __ }
in_ = @{ "in" ~ __ }
notin = @{ "notin" ~ __ }
subset = @{ "subset" ~ __ }
not = @{ "not" ~ __ }
and = @{ "and" ~ __ }
or = @{ "or" ~ __ }
impl_ = @{ "impl" ~ __ }
iff = @{ "iff" ~ __ }
exists = @{ "exists" ~ __ }
forall = @{ "forall" ~ __ }
choose = @{ "choose" ~ __ }
where = @{ "where" ~ __ }

// Non-operator Symbols
// Some of these are technically operators, but it's better
// if they don't produce tokens because they're part of a
// more complex operator, like `percent` or `bollocks`

semicolon = _{ ";" }
colon = _{ ":" }
comma = _{ "," }
b_slash = _{ "\\" }
quote = _{ "\"" }
percent = _{ "%" }
pipe = _{ "|" }
dot = _{ "." }
qst = _{ "?" }

l_brace = _{ "{" }
r_brace = _{ "}" }
l_paren = _{ "(" }
r_paren = _{ ")" }
l_brack = _{ "[" }
r_brack = _{ "]" }

bollocks = _{ ":=" }
dbl_slash = _{ "//" }
dbl_dot = _{ ".." }
arrow = _{ "=>" }

// Operator symbols
// I need to use separate rules for symbols that can be either prefix or infix,
// otherwise the pratt parser has a fit.

at = { "@" }
at_pre = { "@" }
plus = { "+" }
plus_pre = { "+" }
dash = { "-" }
dash_pre = { "-" }
eq = { "=" }
hash = { "#" }
star = { "*" }
slash = { "/" }
lodash = { "_" }
lt = { "<" }
gt = { ">" }
tilde = { "~" }
bang = { "!" }

dbl_eq = { "==" }
dbl_star = { "**" }
dbl_qst = { "??" }
dbl_amp = { "&&" }
dbl_pipe = { "||" }
dbl_lt = { "<<" }
dbl_gt = { ">>" }
bang_eq = { "!=" }
lt_eq = { "<=" }
gt_eq = { ">=" }

// Operator groups

nested_expression = { l_paren ~ expr ~ r_paren }

infix_op          = _{ ident | nested_expression }

prefix_op         = _{ dash_pre | plus_pre | at_pre | hash | bang | not }

tuple_start_op    = _{ at }

null_coal_op      = _{ dbl_qst }

reduce_op         = { percent ~ (infix_op | bin_op) }

exp_op            = _{ dbl_star }

mult_op           = _{ star | slash | mod_ | div | inter }

add_op            = _{ plus | dash | with | less | union_ }

infix_inject      = { dot ~ infix_op }

set_op            = _{ in_ | notin | subset }

compare_op        = _{ lt_eq | lt | gt_eq | gt | dbl_eq | bang_eq }

and_op            = _{ and | dbl_amp }

or_op             = _{ or | dbl_pipe }

impl_op           = _{ impl_ }

iff_op            = _{ iff }

select_op         = _{ exists | forall | choose }

where_op          = _{ where }

bin_op = _{
   | tuple_start_op
   | null_coal_op
   | exp_op
   | mult_op
   | add_op
   | reduce_op
   | infix_inject
   | set_op
   | compare_op
   | and_op
   | or_op
   | impl_op
   | iff_op
}

// Literals

atom_keep = { ( ASCII_ALPHANUMERIC | lodash )+ }
atom = ${
   colon ~ atom_keep
}

number_base = @{ ASCII_DIGIT+ }
number_decimal = @{ (dot ~ ASCII_DIGIT+)? }
number_exp = @{ ((^"e" | ^"f") ~ (plus | dash)? ~ ASCII_DIGIT+)? }
number = ${
   number_base
   ~ number_decimal
   ~ number_exp
}

string_char = {
   !(quote | b_slash) ~ ANY
   | b_slash ~ ( quote | b_slash | "n" | "t" )
}
string_inner = @{ string_char* }
string = ${ quote ~ string_inner ~ quote }

ident = @{ (ASCII_ALPHA | lodash) ~ (ASCII_ALPHANUMERIC | lodash)* }

tuple_literal = { l_brack ~ former? ~ r_brack }

set_literal = { l_brace ~ former? ~ r_brace }

// Expressions

primary = _{
   | null // More ubiquitous than "OM"
   | newat
   | true_
   | false_
   | atom
   | number
   | string
   | tuple_literal
   | set_literal
   | short_func
   | long_func
   | ident // Must follow primary keywords and function literal
   | nested_expression
}

bin_expr = { prefix_op* ~ primary ~ selector* ~ (bin_op ~ prefix_op* ~ primary ~ selector*)* }

assignment_expr = { lhs ~ bollocks ~ expr }

select_expr = { select_op ~ iterator }

expr = _{ 
   | ternary_expr
   | switch_expr
   | select_expr
   | assignment_expr
   | bin_expr
}

expr_block = _{ expr ~ (semicolon ~ expr)* }

captured_semicolon = @{ semicolon }
block_with_capture = _{ l_brace ~ (expr_block ~ captured_semicolon?)? ~ r_brace }

// Special sub-expressions

expr_list = _{ expr ~ (comma ~ expr)* }

range_start = { expr? }
range_end = { expr? }
range = _{ range_start ~ dbl_dot ~ range_end }

// Selectors

fn_call = { l_paren ~ expr_list? ~ r_paren }

range_call = { l_paren ~ range ~ r_paren }

pick_call = { l_brace ~ expr_list ~ r_brace }

selector = _{ fn_call | range_call | pick_call }

// Bound

bound = _{
   | tilde
   | ident
   | (l_brack ~ bound_list ~ r_brack)
}

/*
eg:
~
foo
[foo,bar,~,[baz,far]]
*/

bound_list = { bound ~ (comma ~ bound)* }

// Iterator

in_iterator = { bound_list ~ in_ ~ expr }

select_iterator_single = { bound ~ eq ~ ident ~ l_paren ~ bound_list ~ r_paren }

select_iterator_multi = { bound ~ eq ~ ident ~ l_brace ~ bound_list ~ r_brace }

single_iterator = _{
   | in_iterator
   | select_iterator_single
   | select_iterator_multi
}

iterator_list = { single_iterator ~ (comma ~ single_iterator)* }

iterator = { iterator_list ~ (pipe ~ expr_list)? }

// Formers

iterator_former = { expr ~ colon ~ iterator }

interval_range_former = { expr ~ comma ~ range }

range_former = { range }

literal_former = { expr_list }

former = _{
   | iterator_former
   | interval_range_former // Must come before the literal former
   | range_former
   | literal_former
}

// Function literals

// In the original ISETL 3.0:
// When you reference external variables in a function body, they normally keep their
// reference, so their value in the function is equal to the external variable's value
// when the function is called. By using the `value` list in a function definition, the
// value of the externally referenced variable is locked to what it is at the time the
// function is defined. The default behavior is what I'm used to with closures, at least
// with mutable variables in JS. The behavior with the `value` list isn't replicable
// directly in JS as far as I know, but I like it. I wonder if I'll find a use for it.
// However, I will not use a `value` keyword for it since I don't want to prevent the use
// of a `value` ident in the function body (it's probably pretty common), so I'll replace
// the value statement with special param syntax. I'll leave this segment of grammar here.

// values = { value_op ~ ident ~ (comma ~ ident)* ~ semicolon }

// In the original ISETL 3.0:
// When variables are instantiated in a function, they have lexical scope with retention.
// This means they will keep their value in the global namespace even after the function
// ends, unless you add them to the `local` list. Personally, I don't really like this.
// It's the type of side effect that makes it easy to shoot yourself in the foot. I'm
// going to omit this language feature. All variables instantiated inside a function body
// will have no retention as if they were added to the `local` list, and if you want them
// to persist, they will need to be returned from the function. I will leave this segment
// of grammar here in a comment to explain why it's omitted from my derivative language.

// locals = { "local" ~ ident_list? }

// Big surprise, the JavaScript developer wants different function syntax than what good
// ole' Gary M. Levin came up with. I'm sorry, I can't help myself. I just see the `end`
// keyword and it gives me fits, so sue me. Also, I want to introduce atom literals, so
// using colons to prepend the function shorthand wouldn't have worked. I mean, I don't
// think it would have been ambigious for the parser if I did it right, but it would look
// a little unclear to this developer, and we can't have that, can we?

req_param = ${ ident }
opt_param = ${ ident ~ qst }
locked_param = ${ ident ~ "!" }

// The actual ordering can be handled by the parser
param = _{ 
   | opt_param
   | locked_param
   | req_param // Must be last
}

param_list = { l_paren ~ (param ~ (comma ~ param)*)? ~ r_paren }

short_func = { param_list ~ arrow ~ expr }

long_func = { func ~ param_list ~ block_with_capture }

// Left-Hand Side for assignment

lhs_ident = { ident ~ selector* }

lhs_list_item = _{ tilde | lhs }
lhs_list = { l_brack ~ lhs_list_item ~ (comma ~ lhs_list_item)* ~ r_brack }

lhs = _{
   | lhs_ident
   | lhs_list
}

// Conditionals

ternary_expr = {
   if_
   ~ expr
   ~ qst
   ~ expr
   ~ colon
   ~ expr
}

case_consequence = _{
   | block_with_capture
   | expr
}

case = { (tilde | bin_expr) ~ colon ~ case_consequence }

switch_expr = {
   case_
   ~ nested_expression?
   ~ l_brace
   ~ case ~ (comma ~ case)*
   ~ r_brace
}
